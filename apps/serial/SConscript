Import('env', 'libs', 'incs')

import os

env = env.Clone()
libnames = ['Wire', 'arduino', 'cutest']
env.Append(LIBS=libnames)

libnodes = [libs[n] for n in libnames]
libfiles = env.GetBuildPath(libnodes)
libpaths = list(set([os.path.split(f)[0] for f in libfiles]))
env.Append(LIBPATH=libpaths)

incpaths = list(set([incs[n] for n in libnames]))
env.Append(CPPPATH=incpaths)

obj = env.StaticObject(target='serial.o', source=['serial.c'])
elf = env.Program(target='serial.elf', source=[obj])
objcopy_cmd = "avr-objcopy -O ihex -R .eeprom $SOURCE $TARGET"
hex_ = env.Command('serial.hex', elf, objcopy_cmd)

Return('hex_')
